""" Builds path planning package.
"""

load("@optimus_pip_deps//:requirements.bzl", "requirement")
load("@rules_cc//cc:defs.bzl", "cc_binary", "cc_library", "cc_test")
load("@rules_python//python:defs.bzl", "py_binary", "py_library")
load("//repositories:cc_defs.bzl", "COPTS")
load("//third_party/pytest:defs.bzl", "py_pytest_test")

py_library(
    name = "primitive_generator",
    srcs = [
        "geometry.py",
        "primitive_generator.py",
    ],
    visibility = ["//visibility:public"],
    deps = [
        requirement("casadi"),
        requirement("numpy"),
    ],
)

py_binary(
    name = "primitive_exporter",
    srcs = ["primitive_exporter.py"],
    visibility = ["//visibility:public"],
    deps = [
        ":primitive_generator",
        requirement("numpy"),
    ],
)

cc_library(
    name = "path_planning",
    srcs = [
        "action_set_2d.cc",
        "arastar_planner.cc",
        "astar_planner_internal.cc",
        "grid_2d_planner.cc",
        "grid_utils.cc",
        "planner_algorithm.cc",
        "se2_environment.cc",
        "se2_planner.cc",
    ],
    hdrs = [
        "action_set_2d.h",
        "arastar_grid_2d_planner.h",
        "arastar_planner.h",
        "astar_grid_2d_planner.h",
        "astar_planner.h",
        "astar_planner_internal.h",
        "astar_se2_planner.h",
        "common_utils.h",
        "dstar_lite_grid_2d_planner.h",
        "dstar_lite_planner.h",
        "dstar_lite_se2_planner.h",
        "grid_2d.h",
        "grid_2d_environment.h",
        "grid_2d_planner.h",
        "grid_utils.h",
        "planner_algorithm.h",
        "priority_queue_utils.h",
        "se2_environment.h",
        "se2_planner.h",
    ],
    copts = COPTS,
    visibility = ["//visibility:public"],
    deps = [
        "//optimus:common",
        "@boost//:assert",
        "@boost//:concept",
        "@boost//:concept_check",
        "@boost//:heap",
        "@boost//:preprocessor",
        "@boost//:static_assert",
        "@boost//:type_traits",
        "@boost//:unordered",
        "@eigen",
    ],
)

cc_test(
    name = "tests",
    size = "small",
    srcs = [
        "action_set_2d_tests.cc",
        "arastar_planner_tests.cc",
        "astar_planner_tests.cc",
        "common_utils_tests.cc",
        "dstar_lite_planner_tests.cc",
        "grid_2d_environment_tests.cc",
        "planner_algorithm_tests.cc",
        "planner_environment_mocks.h",
        "planner_integration_tests.cc",
        "se2_environment_tests.cc",
        ":test_primitives",
    ],
    copts = COPTS,
    deps = [
        ":path_planning",
        "@com_google_googletest//:gtest_main",
    ],
)

genrule(
    name = "test_primitives",
    outs = ["test_primitives.cc"],
    cmd = " ".join([
        "$(location :primitive_exporter)",
        "--grid_connectivity=8",
        "--max_angle_idx_diff=1",
        "--min_radius_grid=0.9",
        "--output=$@",
    ]),
    tools = [":primitive_exporter"],
)

py_pytest_test(
    name = "primitive_generator_tests",
    size = "small",
    srcs = [
        "primitive_generator_tests.py",
    ],
    deps = [
        "//optimus/path_planning:primitive_generator",
    ],
)

py_binary(
    name = "generate_benchmark_data",
    srcs = ["generate_benchmark_data.py"],
    data = ["@test_data_csail//file"],
    deps = [
        "@rules_python//python/runfiles",
        requirement("pillow"),
    ],
)

genrule(
    name = "benchmark_data",
    outs = ["scsail.png"],
    cmd = " ".join([
        "$(location :generate_benchmark_data)",
        "--output_dir=`dirname $@`",
    ]),
    tools = [":generate_benchmark_data"],
)

genrule(
    name = "benchmark_primitives",
    outs = ["benchmark_primitives.cc"],
    cmd = " ".join([
        "$(location :primitive_exporter)",
        "--grid_connectivity=32",
        "--max_angle_idx_diff=2",
        "--min_radius_grid=4.95",
        "--output=$@",
    ]),
    tools = [":primitive_exporter"],
)

# Profiling:
# bazel run //optimus/path_planning:benchmark_planners --config=profile --run_under="perf record --call-graph dwarf -o `pwd`/perf.data -F 10000" -- --benchmark_filter=BenchmarkGrid2dPlanner/AStar/*
cc_binary(
    name = "benchmark_planners",
    srcs = [
        "benchmark_planners.cc",
        ":benchmark_primitives",
    ],
    copts = COPTS,
    data = [":benchmark_data"],
    deps = [
        ":path_planning",
        "@com_github_google_benchmark//:benchmark",
        "@stb//:stb_image",
    ],
)
